#!/bin/csh -f
#filename   ::  G09_Environment
#Author     ::  Igor Ying Zhang, Xin Xu
#Purpose    ::  Setting G09 running environment and run g09
#Usage      ::  G09_Environment G09InpFile
#Version    ::  V1.1(20100606)
#History    ::  V1.1 :: To take the direction of chk file into acount

setenv g09root "/share/apps/G09B01"
setenv LD_LIBRARY_PATH "$g09root/g09"
setenv GAUSS_EXEDIR "$g09root/g09:$g09root/g09/bsd"

#set scratchdir
set TDWDD="/state/partition1/$USER/scratch"
test -d $TDWDD || mkdir -p $TDWDD
setenv GAUSS_SCRDIR $TDWDD

set FileName=$1:r
set PostFix=$1:e
set CurrDir=$1:h

#
if ($PostFix == 'com' || $PostFix == 'gjf' || $PostFix == 'inp') then
    # At first, set the chk file into scratch in the node
    set cont    = `grep -c "%chk" $1`
    set chkdir  = ""
    set chkname = ""
    if ($cont == 0) then
       echo "No %chk statement"
       $g09root/g09/g09 $1
    else
       if ($cont > 1) then
             echo "More than one '%chk' statements"
             echo "Warning :: handle the chk file in the default direction,"
             echo "           which might be on the NFS resulting in increasing the burden of IO dramatically"
             #run g09
             $g09root/g09/g09 $1
         else
             set chkfile = `grep -i "%chk" $1|cut -f2 -d'='`
             set chkdir  = $chkfile:h
             set chkname = $chkfile:t
             set chkroot = $chkname:r
             if ( -e $chkroot.chk) then
                 cp $chkroot.chk $TDWDD/
             endif
             if ($chkdir == $chkname) then
                 set chkdir = ""
             endif
             set newchkroot = $TDWDD/$chkroot
             echo "chkroot is "$chkroot", and newchkroot is "$newchkroot
             ./ChkReplace.py $1 $chkroot $newchkroot
             #run g09
             $g09root/g09/g09 $1
             cp $newchkroot.chk $chkroot.chk
         endif
    endif
else
    if ($PostFix == 'chk') then
        #formchk chk file
        if (-e $1) then
            $g09root/g09/formchk $1
        else
            echo "The chk file "$1 "doesn\'t exist"
        endif
    endif
endif
#
#Clean the scratch
rm -rf $TDWDD
